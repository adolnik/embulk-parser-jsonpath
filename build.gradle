plugins {
    id "java"
    id "checkstyle"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.4.2"
}
repositories {
    mavenCentral()
    jcenter()
}

group = "com.github.hiroyuki_sato"
version = "0.3.2"
description = "Parses JSON files with JSONPath read by other file input plugins."

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.26"
    compileOnly "org.embulk:embulk-spi:0.10.26"

    compile("org.embulk:embulk-util-config:0.2.+") {
        // They conflict with embulk-core. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.19.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }
    compile "org.embulk:embulk-util-timestamp:0.2.1"
    compile('org.embulk:embulk-util-json:0.1.0') {
        exclude group: "org.msgpack", module: "msgpack-core"
    }

    compile('com.jayway.jsonpath:json-path:2.4.0') {
        exclude group: "org.slf4j", module: "slf4j-api"
    }

    // They are once excluded from transitive dependencies of other dependencies,
    // and added explicitly with versions exactly the same with embulk-core:0.10.19.
    compile "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-core:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"
    compile "javax.validation:validation-api:1.1.0.Final"
    //compile "joda-time:joda-time:2.9.2"

    compile "org.embulk:embulk-util-file:0.1.1"
    compile "org.embulk:embulk-util-timestamp:0.2.1"

    testCompile "junit:junit:4.+"
    testCompile  "org.embulk:embulk-core:0.10.26:tests"
    testCompile  "org.embulk:embulk-standards:0.10.26"

    // TODO: Remove them.
    // These `testCompile` are a tentative workaround. It will be covered in Embulk core's testing mechanism.
    testCompile "org.embulk:embulk-deps:0.10.26"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

embulkPlugin {
    mainClass = "org.embulk.parser.jsonpath.JsonpathParserPlugin"
    category = "parser"
    type = "jsonpath"
}

// TODO: enable when the pure-java plugin publish.
//publishing {
//    publications {
//        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
//            from components.java  // Must be "components.java". The dependency modification works only for it.
//        }
//    }
//    repositories {
//        maven {
//            url = "${project.buildDir}/mavenPublishLocal"
//        }
//    }
//}

gem {
    authors = [ "Hiroyuki Sato","Takuma kanari" ]
    email = [ "hiroysato@gmail.com","chemtrails.t@gmail.com"]
    // "description" of the gem is copied from "description" of your Gradle project.
    summary = "JSON parser with JSONPath plugin for Embulk"
    homepage = "https://github.com/hiroyuki-sato/embulk-parser-jsonpath"
    licenses = [ "MIT" ]

    generateRubyCode = false  // Avoiding generate lib/embulk/parser/jsonpath.rb
    into("lib/embulk/parser/") {
      from "lib/embulk/parser/jsonpath.rb"
    }
    into("lib/embulk/guess/") {
      from "lib/embulk/guess/jsonpath.rb"
    }
    dependencies = [
      "'jsonpath', ['~> 0.5']",
      "'json', ['~> 2.0']"
    ]
}

gemPush {
    host = "https://rubygems.org"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}


// For avoiding the following exception
// Caused by:
// java.lang.OutOfMemoryError: Java heap space
test {
  maxHeapSize = "2g"
}
